#!perl

# AUTHORITY
# DATE
# DIST
# VERSION

use strict;
use warnings;

use File::Spec;
use Getopt::Long qw(:config gnu_getopt no_ignore_case);

sub quickabspath {
    defined $ENV{PWD} && length($ENV{PWD})
        or die "quickabspath: PWD not defined or empty, we need it\n";
    File::Spec->rel2abs($_[0], $ENV{PWD});
}

sub parse_cmdline {
    my $res = GetOptions(
        'help|h' => sub {
            print <<USAGE;
Usage:
  quickabspath [OPTIONS]... <FILE>...
  quickabspath --version (-v)
  quickabspath --help (-h, -?)

Options:

For more details, see the manpage/documentation.
USAGE
            exit 0;
        },
        'version|v' => sub {
            no warnings 'once';
            print "quickabspath version ".($main::VERSION // 'dev').
                "\n";
            exit 0;
        },
    );
    if ($res) {
        if (!@ARGV) {
            warn "quickabspath: Please specify one or more files\n";
            $res = 0;
        }
    }
    exit 99 if !$res;
}

sub run {
    require Cwd;
    for my $path (@ARGV) {
        print quickabspath($path), "\n";
    }
}

# MAIN

parse_cmdline();
run();

1;
# ABSTRACT: Print the absolute path, without resolving symlinks
# PODNAME:

=head1 SYNOPSIS

 % quickabspath [OPTION]... <FILE>...

To demonstrate how C<realpath>, C<quickabspath>, and C<relpath> give you
different results:

 % pwd
 /home/ujang

 % mkdir dir1
 % ln -s dir1 sym1

 % cd sym1

 % realpath .      ;# gives absolute path and resolve symlinks
 /home/ujang/dir1
 % abspath .       ;# an alias for realpath
 /home/ujang/dir1

 % quickabspath .  ;# gives absolute path but does not resolve symlinks
 /home/ujang/sym1

 % relpath /home/ujang/dir1
 .
 % relpath /home/ujang/sym1
 ../sym1


=head1 DESCRIPTION

This is like the Unix utility B<realpath>, but without resolving symbolic links.
It requires C<PWD> environment variable from the shell. It then just uses
L<File::Spec>'s C<rel2abs> against the specified path. This means if the path is
already an absolute path, nothing will be done (no checking that path elements
exist, no resolving of symbolic links).


=head1 OPTIONS


=head1 EXIT CODES

0 on success.

99 on command-line options error.


=head1 FAQ


=head1 SEE ALSO

Unix utility B<realpath>

L<realpath>, L<relpath>

=cut
